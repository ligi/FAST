buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.3'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath('com.stanfy.spoon:spoon-gradle-plugin:0.14.1') {
            // https://github.com/stanfy/spoon-gradle-plugin/issues/33
            exclude module: 'guava'
        }

    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'spoon'

repositories {
    jcenter()
}

android {
    compileSdkVersion 22
    buildToolsVersion "22"

    defaultConfig {
        versionCode 61
        versionName "6.1"
        // this can be 5 but for assertj this fails - only a problem for tests
        // http://stackoverflow.com/questions/25753751/increase-minsdk-for-androidtest
        minSdkVersion 7
        targetSdkVersion 22
        applicationId "org.ligi.fast"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true

            proguardFile 'proguard-project.txt'

            applicationVariants.all { variant ->
                variant.outputs.each { output ->

                    def file = output.outputFile
                    output.outputFile = new File(file.parent, file.name.replace  \
(".apk", "-" + defaultConfig.versionName + ".apk"))
                }
            }
        }
    }

    flavorDimensions "market", "size"
    productFlavors {
        // no google-play services & google-analytics
        forGooglePlay {
            flavorDimension "market"
        }

        forAmazon {
            flavorDimension "market"
        }

        forFDroid {
            flavorDimension "market"
        }

        withExtras {
            flavorDimension "size"
            applicationId "org.ligi.fast"
        }

        noExtras {
            flavorDimension "size"
            applicationId "org.ligi.faster"
        }
    }

    lintOptions {
        disable "MissingTranslation"
        disable "TypographyEllipsis"
        disable "IconLocation"
        disable "IconDensities"
        disable "ContentDescription"
    }

    // needed for assertJ
    packagingOptions {
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'
    }
}

dependencies {
    compile 'org.ligi:tracedroid:1.4'

    androidTestCompile 'com.android.support:support-annotations:20.0.0'
    androidTestCompile 'com.squareup.spoon:spoon-client:1.1.0'
    androidTestCompile 'com.squareup.assertj:assertj-android:1.0.0'

    testCompile 'com.squareup.assertj:assertj-android:1.0.0'
    testCompile 'junit:junit:4.11'
}
