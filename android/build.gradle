apply plugin: 'com.android.application'
apply plugin: 'spoon'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'kotlin-android'

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.google.com' }
}

android {
    compileSdkVersion 26
    buildToolsVersion "25.0.3"

    defaultConfig {
        versionCode 67
        versionName "6.7"
        // this can be 5 but for assertj this fails - only a problem for tests
        // http://stackoverflow.com/questions/25753751/increase-minsdk-for-androidtest
        minSdkVersion 9
        targetSdkVersion 26
        applicationId "org.ligi.fast"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        archivesBaseName = "Fast-$versionName"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFile 'proguard-project.txt'
        }
    }

    flavorDimensions "market", "size"
    productFlavors {
        // no google-play services & google-analytics
        forGooglePlay {
            dimension "market"
        }

        forAmazon {
            dimension "market"
        }

        forFDroid {
            dimension "market"
        }

        withExtras {
            dimension "size"
            applicationId "org.ligi.fast"
        }

        noExtras {
            dimension "size"
            applicationId "org.ligi.faster"
        }
    }

    android.variantFilter { variant ->
        def market = variant.getFlavors().get(0).name
        def size = variant.getFlavors().get(1).name
        variant.setIgnore(project.hasProperty("singleFlavor") && ((market != 'forGooglePlay') || (size != 'noExtras')))
    }

    // ugly but the best workaround I found for getting the permission for spoon screenshot writing to SD-Card
    // http://stackoverflow.com/questions/25276537/how-do-i-change-android-permission-settings-when-running-tests
    sourceSets {
        debug {
            manifest.srcFile 'src/androidTest/AndroidManifest.xml'
        }
    }

    lintOptions {
        disable "MissingTranslation"
        disable "TypographyEllipsis"
        disable "IconLocation"
        disable "IconDensities"
        disable "ContentDescription"
    }

    // needed for assertJ
    packagingOptions {
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'
    }
}

dependencies {
    compile 'com.github.ligi:tracedroid:1.4'

    androidTestCompile 'com.github.ligi:trulesk:0.21'
    androidTestCompile 'com.android.support:support-annotations:26.0.2'
    androidTestCompile 'com.squareup.spoon:spoon-client:1.7.1'
    androidTestCompile 'com.squareup.assertj:assertj-android:1.2.0'

    testCompile 'com.squareup.assertj:assertj-android:1.2.0'
    testCompile 'junit:junit:4.12'
}

spoon {
    debug = true
    grantAllPermissions = true
}