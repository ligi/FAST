buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.2'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:0.10.0'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'spoon'

repositories {
    jcenter()
}

android {
    compileSdkVersion 20
    buildToolsVersion "20"

    defaultConfig {
        versionCode 57
        versionName "5.7"
        // this can be 5 but for assertj this fails - only a problem for tests
        // http://stackoverflow.com/questions/25753751/increase-minsdk-for-androidtest
        minSdkVersion 7
        targetSdkVersion 20
        applicationId "org.ligi.fast"
    }

    buildTypes {
        release {
            runProguard true
            proguardFile 'proguard-project.txt'
            applicationVariants.all { variant ->
                def file = variant.outputFile
                variant.outputFile = new File(file.parent, file.name.replace(".apk", "-" + defaultConfig.versionName + ".apk"))
            }
        }
    }

    flavorDimensions "market","size"
    productFlavors {
        // no google-play services & google-analytics
        forGooglePlay {
            flavorDimension "market"
        }

        forAmazon {
            flavorDimension "market"
        }

        forFDroid {
            flavorDimension "market"
        }

        withExtras {
            flavorDimension "size"
            applicationId "org.ligi.fast"
        }

        noExtras {
            flavorDimension "size"
            applicationId "org.ligi.faster"
        }
    }

    lintOptions {
        disable "MissingTranslation"
        disable "TypographyEllipsis"
        disable "IconLocation"
        disable "IconDensities"
        disable "ContentDescription"
    }

    // needed for assertJ
    packagingOptions {
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'
    }
}

dependencies {
    compile 'org.ligi:tracedroid:1.4'

    androidTestCompile 'com.squareup.spoon:spoon-client:1.1.0'
    androidTestCompile 'com.squareup.assertj:assertj-android:1.0.0'
}
