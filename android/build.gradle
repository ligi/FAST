apply plugin: 'com.android.application'
apply plugin: 'spoon'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'kotlin-android'

repositories {
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.google.com' }
    maven { url "https://plugins.gradle.org/m2/" }
}

android {
    namespace 'org.ligi.fast'
    compileSdkVersion 33

    defaultConfig {
        versionCode 68
        versionName "6.8"
        // this can be 5 but for assertj this fails - only a problem for tests
        // http://stackoverflow.com/questions/25753751/increase-minsdk-for-androidtest
        minSdkVersion 9
        targetSdkVersion 32
        applicationId "org.ligi"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        archivesBaseName = "Fast-$versionName"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFile 'proguard-project.txt'
        }
        debug {
            applicationIdSuffix '.debug'
        }
    }

    flavorDimensions "market", "size"
    productFlavors {
        // no google-play services & google-analytics
        forGooglePlay {
            dimension "market"
        }

        forAmazon {
            dimension "market"
        }

        forFDroid {
            dimension "market"
        }

        withExtras {
            dimension "size"
            applicationIdSuffix ".fast"
        }

        noExtras {
            dimension "size"
            applicationIdSuffix ".faster"
        }
    }

    android.variantFilter { variant ->
        def market = variant.getFlavors().get(0).name
        def size = variant.getFlavors().get(1).name
        variant.setIgnore(project.hasProperty("singleFlavor") && ((market != 'forGooglePlay') || (size != 'noExtras')))
    }

    lint {
        disable 'MissingTranslation', 'TypographyEllipsis', 'IconLocation', 'IconDensities', 'ContentDescription'
    }

    // needed for assertJ
    packagingOptions {
        resources {
            excludes += ['asm-license.txt', 'LICENSE', 'NOTICE']
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    //TODO update raises minSdkLevel
    implementation 'com.github.ligi:tracedroid:1.4'

    //TODO update raises minSdkLevel and requires AndroidX
    androidTestImplementation 'com.github.ligi:trulesk:0.21'
    //TODO deprecated?
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    //TODO deprecated?
    androidTestImplementation 'com.squareup.spoon:spoon-client:1.7.1'
    //TODO deprecated
    androidTestImplementation 'com.squareup.assertj:assertj-android:1.2.0'

    //TODO deprecated
    testImplementation 'com.squareup.assertj:assertj-android:1.2.0'
    //TODO migrate to v5
    testImplementation 'junit:junit:4.13.2'
}

spoon {
    debug = true
    grantAllPermissions = true
}
